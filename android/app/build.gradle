plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

android {
    namespace "com.gercorridas.app"
    compileSdk flutter.compileSdkVersion
    ndkVersion "27.0.12077973"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    signingConfigs {
        release {
            def keyProperties = new Properties()
            def keyPropertiesFile = rootProject.file("key.properties")
            if (keyPropertiesFile.exists()) {
                keyProperties.load(new FileInputStream(keyPropertiesFile))
            }
            keyAlias keyProperties['keyAlias']
            keyPassword keyProperties['keyPassword']
            storeFile file(keyProperties['storeFile'])
            storePassword keyProperties['storePassword']
        }
    }

    defaultConfig {
        applicationId "com.gercorridas.app"
        minSdkVersion flutter.minSdkVersion
        targetSdk flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.release
        }
    }
}

flutter {
    source '../..'
}

tasks.register('renameReleaseApk') {
    group = 'build'
    dependsOn 'assembleRelease'
    doLast {
        def manifestText = file('src/main/AndroidManifest.xml').getText('UTF-8')
        def m = (manifestText =~ /android:label="([^"]+)"/)
        def label = m.find() ? m.group(1) : 'App'

        def pubspecText = rootProject.file('../pubspec.yaml').getText('UTF-8')
        def vm = (pubspecText =~ /version:\s*([\w\.\-\+]+)/)
        def versionRaw = vm.find() ? vm.group(1) : '0.0.0+1'
        def version = versionRaw.split('\\+')[0]

        def timestamp = new java.text.SimpleDateFormat('yyyyMMdd_HHmmss').format(new Date())
        def newName = "${label}-${version}-${timestamp}.apk"

        def outputsDir = layout.buildDirectory.dir('outputs/apk/release').get().asFile
        def standardApk = new File(outputsDir, 'app-release.apk')
        if (standardApk.exists()) {
            copy {
                from standardApk.getParentFile()
                include standardApk.getName()
                into outputsDir
                rename { String _ -> newName }
            }
        }

        def flutterApkDir = rootProject.layout.buildDirectory.dir('app/outputs/flutter-apk').get().asFile
        def flutterApk = new File(flutterApkDir, 'app-release.apk')
        if (flutterApk.exists()) {
            copy {
                from flutterApk.getParentFile()
                include flutterApk.getName()
                into flutterApkDir
                rename { String _ -> newName }
            }
        }
    }
}

afterEvaluate {
    tasks.findByName('assembleRelease')?.finalizedBy(tasks.findByName('renameReleaseApk'))
}
